#pragma once

// Bytecode statement to final statement
.decl BCToFinal(bcStmt: symbol, stmt: symbol)

BCToFinal(bcStmt, stmt) :-
	BytecodeStmt(bcStmt, method, bcIndex),
	Stmt(stmt, method, bcIndex, _).

NextStmt(fromStmt, toStmt) :-
	p1_Next(bcFromStmt, bcToStmt),
	BCToFinal(bcFromStmt, fromStmt),
	BCToFinal(bcToStmt, toStmt).

VarRange(var, startIndex, endIndex) :-
	p1_Var(method, var, _, _),
	p1_VarRange(var, bcStartIndex, bcEndIndex),
	Stmt(_, method, bcStartIndex, startIndex),
	Stmt(_, method, bcEndIndex, endIndex).

Operator(stmt, op) :- p1_Operator(bcStmt, op), BCToFinal(bcStmt, stmt).

VAR(method, ssaVar, type, ssaName, ogName) :-
	p1_Var(method, var, type, ogName),
	p2_Assign(_, var, ssaVar, _, _),
	p2_VarInfo(ssaVar, ssaName, var).

VAR(method, var, type, name, name) :-
	p1_Var(method, var, type, name),
	!p2_Assign(_, var, _, _, _).

ASSIGN(stmt, ssaTo) :-
	p2_Assign(bcStmt, _, ssaTo, _, _),
	BCToFinal(bcStmt, stmt).

Operand_VAR(stmt, pos, oper) :-
	p2_Operand(bcStmt, pos, oper),
	BCToFinal(bcStmt, stmt),
	VAR(_, oper, _, _, _).

Operand_CONST(stmt, pos, oper, type) :-
	p2_Operand(bcStmt, pos, oper),
	p1_Operand(bcStmt, pos, tDesc, oper),
	BCToFinal(bcStmt, stmt),
	!VAR(_, oper, _, _, _),
	DESC2T(type, tDesc).

SLOAD(stmt, fld) :- p1_Sload(bcStmt, fld), BCToFinal(bcStmt, stmt).

RET(stmt) :- p1_Ret(bcStmt), BCToFinal(bcStmt, stmt).

RET_VOID(stmt) :- p1_RetVoid(bcStmt), BCToFinal(bcStmt, stmt).

IF(stmt, jumpStmt) :-
	p1_If(bcStmt, bcJumpStmt),
	BCToFinal(bcStmt, stmt),
	BCToFinal(bcJumpStmt, jumpStmt).

GOTO(stmt, jumpStmt) :-
	p1_Goto(bcStmt, bcJumpStmt),
	BCToFinal(bcStmt, stmt),
	BCToFinal(bcJumpStmt, jumpStmt).

ALLOC(stmt, ssaTo, heap, type) :-
	p1_Alloc(bcStmt, to, heap, type),
	p2_Assign(bcStmt, to, ssaTo, _, _),
	BCToFinal(bcStmt, stmt).

VCALL(stmt, ssaBase, sig) :-
	p1_VCall(bcStmt, base, sig),
	p2_Assign(bcStmt, base, ssaBase, _, _),
	BCToFinal(bcStmt, stmt).

SPCALL(stmt, ssaBase, sig) :-
	p1_SPCall(bcStmt, base, sig),
	p2_Assign(bcStmt, base, ssaBase, _, _),
	BCToFinal(bcStmt, stmt).

SCALL(stmt, sig) :- p1_SCall(bcStmt, sig), BCToFinal(bcStmt, stmt).

INIT_CALL(stmt) :- p1_InitCall(bcStmt), BCToFinal(bcStmt, stmt).

THROW(stmt, ssaVar) :-
	p1_Throw(bcStmt, var),
	p2_Assign(bcStmt, var, ssaVar, _, _),
	BCToFinal(bcStmt, stmt).

EXCEPTION_HANDLER(method, fromStmt, toStmt, handlerStmt, type, var) :-
	p1_ExceptionHandler(method, bcFromStmt, bcToStmt, bcHandlerStmt, type, var),
	BCToFinal(bcFromStmt, fromStmt),
	BCToFinal(bcToStmt, toStmt),
	BCToFinal(bcHandlerStmt, handlerStmt).