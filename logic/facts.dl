#include "schema.dl"

BinOpcode("X-add", "+").
BinOpcode("X-sub", "-").
BinOpcode("X-mul", "*").
BinOpcode("X-div", "/").
BinOpcode("X-rem", "%").
BinOpcode("X-band", "&").
BinOpcode("X-bor", "|").
BinOpcode("X-bxor", "^").
BinOpcode("X-shl", "<<").
BinOpcode("X-shr", ">>").
BinOpcode("X-ushr", ">>>").
BinOpcode("lcmp", "cmp").
BinOpcode("cmpl", "cmp").
BinOpcode("cmpg", "cmp").
UnaryOpcode("X-neg", "-").

ConstLoadOpcode("X-const").
ConstLoadOpcode("ldc").
ConstLoadOpcode("bipush").
ConstLoadOpcode("sipush").
CallOpcode("invokevirtual").
CallOpcode("invokespecial").
CallOpcode("X-invokeinit").

ZeroCondJumpOpcode("ifeq", "==").
ZeroCondJumpOpcode("ifne", "!=").
ZeroCondJumpOpcode("iflt", "<").
ZeroCondJumpOpcode("ifge", ">=").
ZeroCondJumpOpcode("ifgt", ">").
ZeroCondJumpOpcode("ifle", "<=").
NullCondJumpOpcode("ifnull", "==").
NullCondJumpOpcode("ifnonnull", "!=").
CondJumpOpcode("if_icmpeq", "==").
CondJumpOpcode("if_icmpne", "!=").
CondJumpOpcode("if_icmplt", "<").
CondJumpOpcode("if_icmpge", ">=").
CondJumpOpcode("if_icmpgt", ">").
CondJumpOpcode("if_icmple", "<=").
CondJumpOpcode("if_acmpeq", "==").
CondJumpOpcode("if_acmpne", "!=").

.decl __ToSymbol(stack: Stack, out: symbol)
.decl __REQ_StackForNext(stmt: symbol, stack: Stack)
.decl __REQ_Var(stmt: symbol, stack: Stack)
.decl __RES_Var(stmt: symbol, var: symbol)
.decl __REQ_ActualParam(stmt: symbol, pos: number, stack: Stack)
.decl __REQ_Assign(stmt: symbol, to: symbol)
.decl __REQ_Operand(stmt: symbol, pos: number, value: symbol)
.decl __Assign(stmt: symbol, to: symbol)
.decl __Operand(stmt: symbol, pos: number, from: symbol)

StackBeforeStmt(stmt, nil) :- Stmt(stmt, _, 0).

__ToSymbol(nil, "=@@=") :-
	StackBeforeStmt(_, nil).
__ToSymbol([head, stack], cat(head, cat(", ", out0))) :-
	StackBeforeStmt(_, [head, stack]), __ToSymbol(stack, out0).
DEBUG(stmt, out) :- StackBeforeStmt(stmt, stack), __ToSymbol(stack, out).

// Match load/store statements to variable scopes
// The first store statement of a variable is one index
// before the variable's scope start
OpcodeExt(stmt, opcode, var) :-
	__Opcode(stmt, opcode, varIndex),
	(opcode = "X-load" ; opcode = "X-store" ; opcode = "X-inc" ; opcode = "X-dec"),
	Stmt(stmt, method, index),
	Var(method, varIndex, var, _, _, startLabel, endLabel),
	Label(method, startLabel, _, startIndex),
	Label(method, endLabel, _, endIndex),
	(startIndex - 1) <= index, index <= endIndex.

// Exception handlers might introduce unnamed variables
OpcodeExt(stmt, opcode, var) :-
	__Opcode(stmt, opcode, varIndex),
	(opcode = "X-load" ; opcode = "X-store"),
	Stmt(stmt, method, _),
	!Var(method, varIndex, _, _, _, _, _),
	var = cat(cat(method, "/"), cat("$throw", varIndex)).

// Assume that the first instruction of a handler is a store
EXCEPTION_HANDLER(method, startStmt, endStmt, handlerStmt, type, var) :-
	Handler(method, startLabel, endLabel, handlerLabel, type),
	Label(method, startLabel, startStmt, _),
	Label(method, endLabel, endStmt, _),
	Label(method, handlerLabel, handlerStmt, _),
	OpcodeExt(handlerStmt, "X-store", var).

IF(stmt, nextStmt),
NEXT(stmt, nextStmt),
OpcodeExt(stmt, opcode, nextStmt) :-
	__Opcode(stmt, opcode, label),
	(ZeroCondJumpOpcode(opcode, _) ;
	 NullCondJumpOpcode(opcode, _) ;
	 CondJumpOpcode(opcode, _)),
	Stmt(stmt, method, _),
	Label(method, label, nextStmt, _).

GOTO(stmt, nextStmt),
NEXT(stmt, nextStmt),
OpcodeExt(stmt, "goto", nextStmt) :-
	__Opcode(stmt, "goto", label),
	Stmt(stmt, method, _),
	Label(method, label, nextStmt, _).

// Anything other than var load/store and jumps
OpcodeExt(stmt, opcode, oper) :-
	__Opcode(stmt, opcode, oper),
	opcode != "X-load",
	opcode != "X-store",
	opcode != "goto",
	!ZeroCondJumpOpcode(opcode, _),
	!NullCondJumpOpcode(opcode, _),
	!CondJumpOpcode(opcode, _).

// The next statement, in sequence, is also a successor in most cases
NEXT(stmt, cat(method, cat("/", to_string(i+1)))) :-
	__Opcode(stmt, opcode, _),
	Stmt(stmt, method, i),
	opcode != "goto",
	opcode != "X-return",
	opcode != "return".

// Propagate `stack` to all successors
StackBeforeStmt(nextStmt, stack) :-
	__REQ_StackForNext(stmt, stack),
	NEXT(stmt, nextStmt).

///////////////////////////////////////////////////////////////

// -> constant value
__REQ_StackForNext(stmt, [const, stack]) :-
	StackBeforeStmt(stmt, stack),
	OpcodeExt(stmt, opcode, const),
	ConstLoadOpcode(opcode).

// -> value
__REQ_StackForNext(stmt, [var, stack]) :-
	StackBeforeStmt(stmt, stack),
	OpcodeExt(stmt, "X-load", var).

// value ->
__REQ_Assign(stmt, var),
__REQ_Operand(stmt, 0, oper),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, [oper, stack]),
	OpcodeExt(stmt, "X-store", var).

// return value ->
RET(stmt),
__Operand(stmt, 0, oper),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, [oper, stack]),
	OpcodeExt(stmt, "X-return", _).

// return void
RET_VOID(stmt),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, stack),
	OpcodeExt(stmt, "return", _).

// -> heap id
__REQ_StackForNext(stmt, [heap, stack]) :-
	StackBeforeStmt(stmt, stack),
	OpcodeExt(stmt, "new", heap).

// Dup should only follow a `new` opcode -- ignore it
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, stack),
	OpcodeExt(stmt, "dup", _).

// Pop call arguments from stack
__REQ_ActualParam(stmt, argc - 1, stack) :-
	StackBeforeStmt(stmt, stack),
	OpcodeExt(stmt, opcode, sig),
	CallOpcode(opcode),
	Invocation(sig, argc, _).

// actual arg1, arg2, ... ->
__REQ_ActualParam(stmt, pos - 1, stack) :-
	__REQ_ActualParam(stmt, pos, [_, stack]), pos >= 0.

ACTUAL_PARAM(stmt, pos, notHeap) :-
	__REQ_ActualParam(stmt, pos, [notHeap, _]), pos >= 0,
	!ALLOC(_, _, notHeap, _).

ACTUAL_PARAM(stmt, pos, var) :-
	__REQ_ActualParam(stmt, pos, [heap, _]), pos >= 0,
	ALLOC(_, var, heap, _).

// receiver object | Class | heap ->
// -> var to store invocation result
__REQ_Var(stmt, stack) :-
	OpcodeExt(stmt, opcode, sig),
	CallOpcode(opcode),
	Invocation(sig, _, retType),
	(retType != "void" ; opcode = "X-invokeinit"),
	__REQ_ActualParam(stmt, -1, [_, stack]).

__REQ_StackForNext(stmt, stack) :-
	OpcodeExt(stmt, opcode, sig),
	CallOpcode(opcode),
	Invocation(sig, _, "void"), opcode != "X-invokeinit",
	__REQ_ActualParam(stmt, -1, [_, stack]).

// Pop receiver object from stack
ALLOC(allocStmt, base, heap, type),
SPCALL(stmt, base, sig) :-
	OpcodeExt(stmt, "X-invokeinit", sig),
	__REQ_ActualParam(stmt, -1, [heap, _]),
	OpcodeExt(allocStmt, "new", heap),
	__RES_Var(stmt, base),
	AllocType(allocStmt, type).

// Pop receiver object from stack
SPCALL(stmt, base, sig) :-
	OpcodeExt(stmt, "invokespecial", sig),
	__REQ_ActualParam(stmt, -1, [base, _]).

// Pop receiver object from stack
VCALL(stmt, base, sig) :-
	OpcodeExt(stmt, "invokevirtual", sig),
	__REQ_ActualParam(stmt, -1, [base, _]).

// Store the top of the stack into a var
// Found in source code
__RES_Var(stmt, var) :-
	__REQ_Var(stmt, _),
	NEXT(stmt, nextStmt),
	OpcodeExt(nextStmt, "X-store", var).

// ...or generate a temp one
__RES_Var(stmt, var) :-
	__REQ_Var(stmt, _),
	Stmt(stmt, method, i),
	NEXT(stmt, nextStmt),
	OpcodeExt(nextStmt, opcode, _),
	opcode != "X-store",
	var = cat(cat(method, "/"), cat("$r", to_string(i))).

// Handle all assignments to temp or source code vars
__REQ_Assign(stmt, var),
__REQ_StackForNext(stmt, [var, stack]) :-
	__REQ_Var(stmt, stack),
	__RES_Var(stmt, var).

// Compute an intermediate result
// Swap order of operands due to stack semantics
OPERATOR(stmt, op),
__Operand(stmt, 0, oper0),
__Operand(stmt, 1, oper1),
__REQ_Var(stmt, stack) :-
	StackBeforeStmt(stmt, [oper1, [oper0, stack]]),
	OpcodeExt(stmt, opcode, _),
	BinOpcode(opcode, op).

OPERATOR(stmt, op),
__Operand(stmt, 0, oper),
__REQ_Var(stmt, stack) :-
	StackBeforeStmt(stmt, [oper, stack]),
	OpcodeExt(stmt, opcode, _),
	UnaryOpcode(opcode, op).

//// Handle ++ or --
//ASSIGN(stmt, var),
//OPERATOR(stmt, op),
//OPERAND(stmt, 0, var),
//OPERAND(stmt, 1, "1"),
//__REQ_StackForNext(stmt, stack) :-
//	StackBeforeStmt(stmt, stack),
//	((OpcodeExt(stmt, "X-inc", var), op = "+") ;
//	 (OpcodeExt(stmt, "X-dec", var), op = "-")).
//
//// Load static field
//OPERAND(stmt, 0, oper1),
//__REQ_Var(stmt, stack) :-
//	StackBeforeIndex(stmt, stack),
//	OpcodeExt(stmt, "getstatic", oper1).
//
//PopOpcode("pop").
//PopOpcode("pop2").
//__StackForNext(method, i, stack) :-
//	StackBeforeIndex(method, i, [_, stack]),
//	OpcodeExt(method, i, opcode, _),
//	PopOpcode(opcode).

// Handle conditional jumps
OPERATOR(stmt, op),
__Operand(stmt, 0, oper0),
__Operand(stmt, 1, oper1),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, [oper0, stack]),
	OpcodeExt(stmt, opcode, _),
	((ZeroCondJumpOpcode(opcode, op), oper1 = "0") ;
	 (NullCondJumpOpcode(opcode, op), oper1 = "NULL")).

OPERATOR(stmt, op),
__Operand(stmt, 0, oper0),
__Operand(stmt, 1, oper1),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, [oper1, [oper0, stack]]),
	OpcodeExt(stmt, opcode, _),
	CondJumpOpcode(opcode, op).

// ... and unconditional ones
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, stack),
	OpcodeExt(stmt, "goto", _).

// Prepare stack for an exception handler
StackBeforeStmt(handlerStmt, [type, nil]) :-
	EXCEPTION_HANDLER(_, _, _, handlerStmt, type, _).

THROW(stmt, var),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeStmt(stmt, [var, stack]),
	OpcodeExt(stmt, "athrow", _).

///////////////////////////////////////////////////////////////

// Approve all assignments, except self-assign ones
__Assign(stmt, to) :-
	__REQ_Assign(stmt, to),
	!__REQ_Operand(stmt, 0, to).

__Operand(stmt, pos, oper) :-
	__REQ_Operand(stmt, pos, oper),
	__Assign(stmt, _).

ACTUAL_RET(stmt, var) :-
	__REQ_Assign(stmt, var),
	OpcodeExt(stmt, opcode, _),
	CallOpcode(opcode).
