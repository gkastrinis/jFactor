// Output
.decl ASSIGN(stmt: symbol, to: symbol)
.decl VCALL(stmt: symbol, base: symbol, sig: symbol)
.decl SPCALL(stmt: symbol,  base: symbol, sig: symbol)
.decl ALLOC(stmt: symbol, to: symbol, heap: symbol, type: symbol)
.decl IF(stmt: symbol, jumpStmt: symbol)
.decl GOTO(stmt: symbol, jumpStmt: symbol)
.decl RET(stmt: symbol)
.decl RET_VOID(stmt: symbol)
.decl OPERAND(stmt: symbol, pos: number, from: symbol)
.decl OPERATOR(stmt: symbol, op: symbol)
.decl ACTUAL_PARAM(stmt: symbol, pos: number, op: symbol)
.decl ACTUAL_RET(stmt: symbol, var: symbol)
.decl NEXT(fromStmt: symbol, toStmt: symbol)

.output ASSIGN
.output VCALL
.output SPCALL
.output ALLOC
.output IF
.output GOTO
.output RET
.output RET_VOID
.output OPERAND
.output OPERATOR
.output ACTUAL_PARAM
.output ACTUAL_RET
//.output NEXT

// Input
.decl Stmt(id: symbol, method: symbol, index: number)
.decl Opcode(stmt: symbol, opcode: symbol, oper: symbol)
.decl Var(method: symbol, varIndex: symbol, var: symbol, name: symbol, type: symbol)
.decl Invocation(sig: symbol, argc: number, retType: symbol)
.decl AllocType(stmt: symbol, type: symbol)
.decl Label(method: symbol, label: symbol, stmt: symbol)
.input Stmt
.input Opcode
.input Var
.input Invocation
.input AllocType
.input Label

// Opcodes
.decl BinOpcode(opcode: symbol, op: symbol)
.decl UnaryOpcode(opcode: symbol, op: symbol)
.decl ConstLoadOpcode(opcode: symbol)
.decl CallOpcode(opcode: symbol)
.decl PopOpcode(opcode: symbol)
.decl ZeroCondJumpOpcode(opcode: symbol, op: symbol)
.decl NullCondJumpOpcode(opcode: symbol, op: symbol)
.decl CondJumpOpcode(opcode: symbol, op: symbol)
.decl JumpOpcode(opcode: symbol)

BinOpcode("X-add", "+").
BinOpcode("X-sub", "-").
BinOpcode("X-mul", "*").
BinOpcode("X-div", "/").
BinOpcode("X-rem", "%").
BinOpcode("X-band", "&").
BinOpcode("X-bor", "|").
BinOpcode("X-bxor", "^").
BinOpcode("X-shl", "<<").
BinOpcode("X-shr", ">>").
BinOpcode("X-ushr", ">>>").
BinOpcode("lcmp", "cmp").
BinOpcode("cmpl", "cmp").
BinOpcode("cmpg", "cmp").
UnaryOpcode("X-neg", "-").

ConstLoadOpcode("X-const").
ConstLoadOpcode("ldc").
ConstLoadOpcode("bipush").
ConstLoadOpcode("sipush").
CallOpcode("invokevirtual").
CallOpcode("invokespecial").
CallOpcode("X-invokeinit").

PopOpcode("pop").
PopOpcode("pop2").

ZeroCondJumpOpcode("ifeq", "==").
ZeroCondJumpOpcode("ifne", "!=").
ZeroCondJumpOpcode("iflt", "<").
ZeroCondJumpOpcode("ifge", ">=").
ZeroCondJumpOpcode("ifgt", ">").
ZeroCondJumpOpcode("ifle", "<=").
NullCondJumpOpcode("ifnull", "==").
NullCondJumpOpcode("ifnonnull", "!=").
CondJumpOpcode("if_icmpeq", "==").
CondJumpOpcode("if_icmpne", "!=").
CondJumpOpcode("if_icmplt", "<").
CondJumpOpcode("if_icmpge", ">=").
CondJumpOpcode("if_icmpgt", ">").
CondJumpOpcode("if_icmple", "<=").
CondJumpOpcode("if_acmpeq", "==").
CondJumpOpcode("if_acmpne", "!=").
JumpOpcode("goto").

///////////////////////////////////////////////////////////////

.type Stack = [top: symbol, rest: Stack]
.decl StackBeforeIndex(stmt: symbol, stack: Stack)
StackBeforeIndex(stmt, nil) :- Stmt(stmt, _, 0).

.decl ToSymbol(stack: Stack, out: symbol)
.decl DEBUG(stmt: symbol, out: symbol)
.output DEBUG
ToSymbol(nil, "=@@=") :- StackBeforeIndex(_, nil).
ToSymbol([head, stack], cat(head, cat(", ", out0))) :- StackBeforeIndex(_, [head, stack]), ToSymbol(stack, out0).
DEBUG(stmt, out) :- StackBeforeIndex(stmt, stack), ToSymbol(stack, out).

.decl __REQ_StackForNext(stmt: symbol, stack: Stack)
.decl __REQ_Var(stmt: symbol, stack: Stack)
.decl __RES_Var(stmt: symbol, var: symbol)
.decl __REQ_ActualParam(stmt: symbol, pos: number, stack: Stack)
.decl __REQ_Assign(stmt: symbol, to: symbol)
.decl __REQ_Operand(stmt: symbol, pos: number, value: symbol)
.decl __HeapToIndex(heap: symbol, stmt: symbol)

///////////////////////////////////////////////////////////////

// -> constant value
__REQ_StackForNext(stmt, [const, stack]) :-
	StackBeforeIndex(stmt, stack),
	Opcode(stmt, opcode, const),
	ConstLoadOpcode(opcode).

// -> value
__REQ_StackForNext(stmt, [var, stack]) :-
	StackBeforeIndex(stmt, stack),
	Opcode(stmt, "X-load", varIndex),
	Stmt(stmt, method, _),
	Var(method, varIndex, var, _, _).

// value ->
__REQ_Assign(stmt, var),
__REQ_Operand(stmt, 0, oper),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeIndex(stmt, [oper, stack]),
	Opcode(stmt, "X-store", varIndex),
	Stmt(stmt, method, _),
	Var(method, varIndex, var, _, _).

// return value ->
RET(stmt),
OPERAND(stmt, 0, oper),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeIndex(stmt, [oper, stack]),
	Opcode(stmt, "X-return", _).

// return void
RET_VOID(stmt),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeIndex(stmt, stack),
	Opcode(stmt, "X-return-void", _).

// -> heap id
__HeapToIndex(heap, stmt),
__REQ_StackForNext(stmt, [heap, stack]) :-
	StackBeforeIndex(stmt, stack),
	Opcode(stmt, "new", heap).

// Dup should only follow a `new` opcode -- ignore it
__REQ_StackForNext(stmt, stack) :-
	StackBeforeIndex(stmt, stack),
	Opcode(stmt, "dup", _).

// Pop call arguments from stack
__REQ_ActualParam(stmt, argc - 1, stack) :-
	StackBeforeIndex(stmt, stack),
	Opcode(stmt, opcode, sig),
	CallOpcode(opcode),
	Invocation(sig, argc, _).

// actual arg1, arg2, ... ->
__REQ_ActualParam(stmt, pos - 1, stack) :-
	__REQ_ActualParam(stmt, pos, [_, stack]), pos >= 0.

ACTUAL_PARAM(stmt, pos, notHeap) :-
	__REQ_ActualParam(stmt, pos, [notHeap, _]), pos >= 0,
	!ALLOC(_, _, notHeap, _).

ACTUAL_PARAM(stmt, pos, var) :-
	__REQ_ActualParam(stmt, pos, [heap, _]), pos >= 0,
	ALLOC(_, var, heap, _).

// receiver object | Class | heap ->
// -> var to store invocation result
__REQ_Var(stmt, stack) :-
	Opcode(stmt, opcode, sig),
	CallOpcode(opcode),
	Invocation(sig, _, retType),
	(retType != "void" ; opcode = "X-invokeinit"),
	__REQ_ActualParam(stmt, -1, [_, stack]).

__REQ_StackForNext(stmt, stack) :-
	Opcode(stmt, opcode, sig),
	CallOpcode(opcode),
	Invocation(sig, _, "void"), opcode != "X-invokeinit",
	__REQ_ActualParam(stmt, -1, [_, stack]).

// Pop receiver object from stack
ALLOC(allocStmt, base, heap, type),
SPCALL(stmt, base, sig) :-
	Opcode(stmt, "X-invokeinit", sig),
	__REQ_ActualParam(stmt, -1, [heap, _]),
	__HeapToIndex(heap, allocStmt),
	__RES_Var(stmt, base),
	AllocType(allocStmt, type).

// Pop receiver object from stack
SPCALL(stmt, base, sig) :-
	Opcode(stmt, "invokespecial", sig),
	__REQ_ActualParam(stmt, -1, [base, _]).

// Pop receiver object from stack
VCALL(stmt, base, sig) :-
	Opcode(stmt, "invokevirtual", sig),
	__REQ_ActualParam(stmt, -1, [base, _]).

// Store the top of the stack into a var
// Found in source code
__RES_Var(stmt, var) :-
	__REQ_Var(stmt, _),
	Stmt(stmt, method, _),
	NEXT(stmt, nextStmt),
	Opcode(nextStmt, "X-store", varIndex),
	Var(method, varIndex, var, _, _).

// ...or generate a temp one
__RES_Var(stmt, var) :-
	__REQ_Var(stmt, _),
	Stmt(stmt, method, i),
	NEXT(stmt, nextStmt),
	Opcode(nextStmt, opcode, _),
	opcode != "X-store",
	var = cat(cat(method, "/"), cat("$r", to_string(i))).

// Handle all assignments to temp or source code vars
__REQ_Assign(stmt, var),
__REQ_StackForNext(stmt, [var, stack]) :-
	__REQ_Var(stmt, stack),
	__RES_Var(stmt, var).

// Compute an intermediate result
// Swap order of operands due to stack semantics
OPERATOR(stmt, op),
__REQ_Operand(stmt, 0, oper2),
__REQ_Operand(stmt, 1, oper1),
__REQ_Var(stmt, stack) :-
	StackBeforeIndex(stmt, [oper1, [oper2, stack]]),
	Opcode(stmt, opcode, _),
	BinOpcode(opcode, op).

OPERATOR(stmt, op),
__REQ_Operand(stmt, 0, oper),
__REQ_Var(stmt, stack) :-
	StackBeforeIndex(stmt, [oper, stack]),
	Opcode(stmt, opcode, _),
	UnaryOpcode(opcode, op).

//// Load static field
//OPERAND(stmt, 0, oper1),
//__REQ_Var(stmt, stack) :-
//	StackBeforeIndex(stmt, stack),
//	Opcode(stmt, "getstatic", oper1).
//
//// TODO might be problematic
//__StackForNext(method, i, stack) :-
//	StackBeforeIndex(method, i, [_, stack]),
//	Opcode(method, i, opcode, _),
//	PopOpcode(opcode).

// Handle conditional jumps
IF(stmt, nextStmt),
OPERATOR(stmt, op),
__REQ_Operand(stmt, 0, oper0),
__REQ_Operand(stmt, 1, oper1),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeIndex(stmt, [oper0, stack]),
	Opcode(stmt, opcode, _),
	((ZeroCondJumpOpcode(opcode, op), oper1 = "0") ;
	 (NullCondJumpOpcode(opcode, op), oper1 = "NULL")),
	NEXT(stmt, nextStmt).

// TODO merge with above
IF(stmt, nextStmt),
OPERATOR(stmt, op),
__REQ_Operand(stmt, 0, oper1),
__REQ_Operand(stmt, 1, oper2),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeIndex(stmt, [oper2, [oper1, stack]]),
	Opcode(stmt, opcode, _),
	CondJumpOpcode(opcode, op),
	NEXT(stmt, nextStmt).

// TODO Move IF and GOTO in computation of NEXT
// ... and unconditional ones
GOTO(stmt, nextStmt),
__REQ_StackForNext(stmt, stack) :-
	StackBeforeIndex(stmt, stack),
	Opcode(stmt, opcode, _),
	JumpOpcode(opcode),
	NEXT(stmt, nextStmt).

///////////////////////////////////////////////////////////////

NEXT(stmt, nextStmt) :-
	Opcode(stmt, opcode, label),
	(ZeroCondJumpOpcode(opcode, _) ;
	 NullCondJumpOpcode(opcode, _) ;
	 CondJumpOpcode(opcode, _) ;
	 JumpOpcode(opcode)),
	Stmt(stmt, method, _),
	Label(method, label, nextStmt).

NEXT(stmt, cat(method, cat("/", to_string(i+1)))) :-
	Opcode(stmt, opcode, _),
	Stmt(stmt, method, i),
	!JumpOpcode(opcode).

StackBeforeIndex(nextStmt, stack) :-
	__REQ_StackForNext(stmt, stack),
	NEXT(stmt, nextStmt).

// Approve all assignments, except self-assign ones
ASSIGN(stmt, to) :-
	__REQ_Assign(stmt, to),
	!__REQ_Operand(stmt, 0, to).

OPERAND(stmt, pos, oper) :-
	__REQ_Operand(stmt, pos, oper),
	(ASSIGN(stmt, _) ; IF(stmt, _)).

ACTUAL_RET(stmt, var) :-
	__REQ_Assign(stmt, var),
	Opcode(stmt, opcode, _),
	CallOpcode(opcode).
