plugins {
	id "application"
	id "groovy"
}

group = "org.codesimius"
mainClassName = "org.codesimius.jfactor.Main"

wrapper() { gradleVersion = "7.4" }

repositories { mavenCentral() }

dependencies {
	implementation "org.codehaus.groovy:groovy-all:3.0.9"
	implementation "org.ow2.asm:asm:9.1"
	implementation "org.ow2.asm:asm-commons:9.1"
	implementation "org.ow2.asm:asm-util:9.1"
}

run {
	if (project.hasProperty("args")) args project.property("args").split()
}

def testNames = new File(this.projectDir, "src/inputTests").list()

sourceSets {
	testNames.each { t -> "$t" { java { srcDirs = ["src/inputTests/$t"] } } }
}

testNames.each {name ->
	task "${name}JAR"(type:Jar, dependsOn: "${name}Classes") {
		group = 'other'
		from sourceSets.getByName(name).output.classesDirs
		archiveFileName.set("${name}.jar")
		manifest { attributes 'Main-Class': "A" }
		run.dependsOn it
	}
}
