plugins {
	id "application"
	id "groovy"
}

group = "org.codesimius"
mainClassName = "org.codesimius.jfactor.Main"

wrapper() { gradleVersion = "6.7" }

repositories { mavenCentral() }

dependencies {
	implementation "org.codehaus.groovy:groovy-all:3.0.6"
	implementation "org.ow2.asm:asm:9.0"
	implementation "org.ow2.asm:asm-commons:9.0"
	implementation "org.ow2.asm:asm-util:9.0"
}

run {
	if (project.hasProperty("args")) args project.property("args").split()
}

def testNames = new File("src/inputTests").list()

sourceSets {
	testNames.each { t -> "$t" { java { srcDirs = ["src/inputTests/$t"] } } }
}

testNames.each {name ->
	task "${name}JAR"(type:Jar, dependsOn: "${name}Classes") {
		group = 'Input Test'
		from sourceSets.getByName(name).output.classesDirs
		archiveFileName.set("${name}.jar")
		manifest { attributes 'Main-Class': "A" }
		run.dependsOn it
	}
}